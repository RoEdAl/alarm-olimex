# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='uboot-olimex-32'
pkgname=('uboot-olimex-a10' 'uboot-olimex-a13' 'uboot-olimex-a20')
_commit=45475c072c870b01639c91cea88c82872db716d5
_srcname=u-boot-olinuxino-${_commit}
_pkgrelease=20200701
pkgver=2020.01.${_pkgrelease}
pkgrel=2
arch=('armv7h')
url='http://github.com/OLIMEX/u-boot-olinuxino'
license=('GPL')
options=('!strip')
makedepends=('bc' 'swig' 'dtc')
backup=(
	'boot/boot.txt'
	'boot/boot.scr'
)

_patches=(
	0001-set-fdtfile-environment-variable.patch
	0002-libfdt-build-fix.patch
	0003-build-correctly-with-CONFIG_ENV_IS_NOWHERE-macro-def.patch
	0004-a20-fix-axp209-configuration.patch
	0005-a10-13-show-board-info-newline-after-device-ID.patch
	0006-avoid-declared-but-not-unused-compiler-warnings.patch
	0007-a10-a13-a64-spl_board_init-be-less-verbose.patch
)

source=(
	"https://github.com/OLIMEX/u-boot-olinuxino/archive/${_commit}.tar.gz"
	${_patches[@]}
	'boot.txt'
	'flash-uboot.sh'
	'config-fragment'
	'mkscr.sh'
)
sha1sums=('dbb0f540a507527edd788b84c62c2d8a4a5c957b'
          'c9ff80216a6fcb468a0b0da42e91c0a39a1aa431'
          'ceb1c2b763ee97924922d3a5c93dfb88929bef78'
          'ba4d4c1d7451a2c8e3428797fe02f0bfa4412304'
          '4382d4e16fe2d2d73fea80e48d8d00cec220c046'
          'a116176239bcb16861d773eceea1431e4132f8c9'
          '581fd7739c7b7b362f0527b4595cb7ca0a4756f1'
          '563e1ceb53c01a2df8bde52331bdcf18fcaaa0cb'
          '9f5d51937b0dde70b21f84dc93a2f770f3488226'
          'a49a8c18e1b0f920a430cf57990c0c42dc864f59'
          '2cd0bd7e1de5e498697ae80b8e3b7c423905b55b'
          '5fbe0039f63552831950ad3eb62cf054d9e6f552')

_boards=('A10-OLinuXino' 'A13-OLinuXino' 'A20-OLinuXino')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done
}

build() {
  cd "${srcdir}/${_srcname}"

  for i in ${_boards[@]}; do
    msg2 ${i}
    mkdir ../bin_${i} || true
    make distclean
    make ${i}_config
    scripts/kconfig/merge_config.sh .config ${srcdir}/config-fragment
    make EXTRAVERSION=.${_pkgrelease}-${pkgrel} VERBOSE=0
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
    case $i in
        A10-OLinuXino)
        mv arch/arm/dts/sun4i-a10-olinuxino-*.dtb ../bin_${i}
        ;;

        A13-OLinuXino)
        mv arch/arm/dts/sun5i-a13-olinuxino-*.dtb ../bin_${i}
        mv arch/arm/dts/sun5i-a13-som-*.dtb ../bin_${i}
        ;;

        A20-OLinuXino)
        mv arch/arm/dts/sun7i-a20-olinuxino-*.dtb ../bin_${i}
        mv arch/arm/dts/sun7i-a20-olimex-*.dtb ../bin_${i}
        ;;
    esac
  done

  tools/mkimage -A arm -O linux -T script -C none -n 'U-Boot boot script' -d ../boot.txt ../boot.scr
}

package_uboot-olimex-a10() {
  pkgdesc='U-Boot for AllWinner A10-based Olimex SBCs'
  install=${pkgbase}.install
  provides=('uboot-olimex')
  conflicts=('uboot-sunxi' 'uboot-olimex')
  optdepends=('uboot-tools')

  install -dm0755 "${pkgdir}/boot/dtbs-extra"
  install -Dm0644 bin_A10-OLinuXino/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm0644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm0644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm0755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm0755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"

  cp bin_A10-OLinuXino/*.dtb "${pkgdir}/boot/dtbs-extra"
}

package_uboot-olimex-a13() {
  pkgdesc='U-Boot for AllWinner A13-based Olimex SBCs'
  install=${pkgbase}.install
  provides=('uboot-olimex')
  conflicts=('uboot-sunxi' 'uboot-olimex' 'uboot-olinuxino')
  optdepends=('uboot-tools')

  install -dm0755 "${pkgdir}/boot/dtbs-extra"
  install -Dm0644 bin_A13-OLinuXino/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm0644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm0644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm0755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm0755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"

  cp bin_A13-OLinuXino/*.dtb "${pkgdir}/boot/dtbs-extra"
}


package_uboot-olimex-a20() {
  pkgdesc='U-Boot for AllWinner A20-based Olimex SBCs'
  install=${pkgbase}.install
  provides=('uboot-olimex')
  conflicts=('uboot-sunxi' 'uboot-olimex')
  optdepends=('uboot-tools')

  install -dm0755 "${pkgdir}/boot/dtbs-extra"
  install -Dm0644 bin_A20-OLinuXino/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm0644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm0644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm0755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm0755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"

  cp bin_A20-OLinuXino/*.dtb "${pkgdir}/boot/dtbs-extra"
}
